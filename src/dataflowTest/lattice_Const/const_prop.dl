.enum Constant = {
	case "Top",
	case "Bot"
}
// notice: enum must be decalred before any use of 
// enum element (because of symbol table offset)


.def lub(x: Constant, y: Constant): Constant {
    case ("Bot", _)   => y,
    case (_, "Bot")   => x,
    case (_, _)	      => x=y ? x : "Top"
}

.def glb(x: Constant, y: Constant): Constant {
    case ("Top", _)   => y,
    case (_, "Top")   => x,
    case (_, _)	      => x=y ? x : "Bot"
}

.def lat_alpha(x: number): Constant {
    case (_)	      => x
}

.def lat_sum(x: Constant, y: Constant): Constant {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Top", _)   => "Top",
    case (_, "Top")   => "Top",
    case (_, _)	      => x+y
}

.def lat_div(x: Constant, y: Constant): Constant {
    case ("Bot", _)   => "Bot",
    case (_, "Bot")   => "Bot",
    case ("Top", _)   => "Top",
    case (_, "Top")   => "Top",
    case (_, _)	      => y=0 ? "Bot" : x/y
}

.let Const<> = ("Bot", "Top", lub, glb)

.decl setConstStm(l:number, r: symbol, c: number)         // r = c
.input setConstStm
.decl addStm(l:number, r: symbol, x: symbol, y: symbol) // r = x + y
.input addStm
.decl divStm(l:number, r: symbol, x: symbol, y: symbol) // r = x / y
.input divStm

.decl flow(l1: number, l2: number) // control flow from l1 to l2
.input flow

.lat varEntry(l:number, k: symbol, v: Constant)
.output varEntry
.lat varExit(l:number, k: symbol, v: Constant)
.output varExit

// varEntry of l2 is the union of {varExit(l1) | flow(l1,l2)}
varEntry(l2, k, v) :- varExit(l1, k, v), flow(l1, l2).

// statement: set to constant number
varExit(l, r, &lat_alpha(c)) :- setConstStm(l, r, c).

// addition statement r = x+y, and the value of x is v1, the
// value of y is v2
varExit(l, r, &lat_sum(v1, v2)) :- addStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2).
// division statement
varExit(l, r, &lat_div(v1, v2)) :- divStm(l, r, x, y),
                            varEntry(l, x, v1),
                            varEntry(l, y, v2).

// if the statement doesn't assign to r
.decl assignVar(l:number, r: symbol) // this statement assign r to a new value
assignVar(l, r) :- setConstStm(l, r, c).
assignVar(l, r) :- addStm(l, r, x, y).
assignVar(l, r) :- divStm(l, r, x, y).
varExit(l, r, v) :- varEntry(l, r, v), !assignVar(l, r).


// lattice relation
//.lat A(x: symbol, s: Constant)
//.input A
//.lat B(x: symbol, s: Constant)
//.input B
//.lat C(x: symbol, s: Constant)
//.input C
//.lat D(x: symbol, s: Constant)
//.input D
//.lat R(x: symbol, s: Constant)
//.output R

//R(x, s) :- A(x, s), B(x, s), C(x, s).
//A(x, s) :- R(x, s), B(x, s).
//R(x, s) :- A(x, s), D(x, s).
//R(x, s) :- A(x, s), C(x, s).

// normal relation
//.decl P(x: symbol, s: symbol)
//.input P
//.decl Q(x: symbol, s: symbol)
//.input Q
//.decl S(x: symbol, s: symbol)
//.output S
//S(x, s) :- P(x, s), Q(x, s).
//P(x, s) :- S(x, s).
//Q(x, s) :- P(x, s).
